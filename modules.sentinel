#
# This module enforces the usage of required modules 
# while prohibiting the creation of the resource those 
# modules contain. For instance, if a required module 
# creates a VPC in AWS, the consumer will not be allowed 
# to create their own VPC. This reduces risk as well as 
# module maintenance. 
#

import "tfconfig/v2" as tfconfig
import "strings"
import "json"
import "http"

param provider
param base_url

# These modules are required to be used
req = http.request(base_url + "/list/required-modules/" + provider)
res = json.unmarshal(http.get(req).body)
required_modules = res

# Get all resources
module_resources = func() {
    missing_modules = []
    all_modules = []
    prohibited_resources = []
    failed_resources = []
    errors = []
    passed = true

    # get a list of resource being used in the required modules
    for tfconfig.resources as res {
        modaddr = tfconfig.resources[res].module_address
        if modaddr != "" {
            modfull = strings.split(modaddr, ".")
            modname = modfull[1]
            if modname not in all_modules {
                append(all_modules, modname)
            }
            if modname in required_modules {
                append(prohibited_resources, tfconfig.resources[res].type)
            }
        }
    }

    for required_modules as mod {
        if mod not in all_modules {
            append(missing_modules, mod)
            passed = false
        }
    }

    # get a list of resources not in the required modules
    # and compare that to resource that are in the required modules
    for tfconfig.resources as res {
        restype = tfconfig.resources[res].type
        modname = tfconfig.resources[res].module_address
        if modname != "" {
            modfull = strings.split(modname, ".")
            modname = modfull[1]
        }
        if modname not in required_modules {
            if restype in prohibited_resources {
                append(failed_resources, restype)
                passed = false
            }
        }
    }

    if length(missing_modules) > 0 {
        append(errors, "Missing required modules")
    }
    if length(failed_resources) > 0 {
        append(errors, "Attempt to create prohibited resources")
    }

    output = {
        "modules": {
            "required_modules": required_modules,
            "missing_modules": missing_modules,
            "all_modules": all_modules,
        },
        "resources": {
            "prohibited_resources": prohibited_resources,
            "failed_resources": failed_resources,
        },
        "status": passed,
        "errors": errors,
    }

    print(json.marshal(output))

    return passed
}

# If true, we passed!
main = rule { module_resources() }
